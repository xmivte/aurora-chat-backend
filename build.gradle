plugins {
	id 'java'
	id 'org.springframework.boot' version '3.3.4'
	id 'io.spring.dependency-management' version '1.1.6'
	id 'com.diffplug.spotless' version '6.25.0'
	id 'com.github.spotbugs' version '5.1.4'
	id 'pmd'
}

group = 'com.example'
version = '0.0.1-SNAPSHOT'

java {
	toolchain {
		languageVersion = JavaLanguageVersion.of(21)
	}
}

jar {
    enabled = false
}

repositories {
	mavenCentral()
}

dependencies {
	implementation 'org.springframework.boot:spring-boot-starter-web'
	testImplementation 'org.springframework.boot:spring-boot-starter-test'
	testRuntimeOnly 'org.junit.platform:junit-platform-launcher'
    implementation 'org.springdoc:springdoc-openapi-starter-webmvc-ui:2.6.0'

    // testing with mockito
	testImplementation 'org.mockito:mockito-core:5.20.0'

	// testing with embedded postgres
	testImplementation platform('io.zonky.test.postgres:embedded-postgres-binaries-bom:17.6.0')
	testImplementation 'io.zonky.test:embedded-postgres:2.1.1'

	// lombok
	compileOnly 'org.projectlombok:lombok'
	annotationProcessor 'org.projectlombok:lombok'

    implementation 'org.mybatis.spring.boot:mybatis-spring-boot-starter:3.0.4'
	testImplementation 'org.mybatis.spring.boot:mybatis-spring-boot-starter-test:3.0.5'
    runtimeOnly 'org.postgresql:postgresql'

    implementation 'org.springframework.boot:spring-boot-starter-websocket'

    implementation 'org.flywaydb:flyway-database-postgresql'

	implementation 'org.apache.commons:commons-lang3:3.20.0'
}

tasks.named('test') {
	useJUnitPlatform()

}

spotless {
	java {
		eclipse()
		target("src/**/*.java")
	}
}
spotbugs {
	toolVersion = '4.8.3'
	effort = 'max'
	reportLevel = 'low'
}

tasks.withType(com.github.spotbugs.snom.SpotBugsTask) {
	reports {
		html.required = true
		xml.required = false
	}
}

pmd {
	toolVersion = '6.55.0'
	ruleSets = ['rulesets/java/basic.xml', 'rulesets/java/unusedcode.xml', 'rulesets/java/imports.xml']
}
tasks.named('check') {
	dependsOn 'spotlessCheck', 'spotbugsMain', 'pmdMain'
}
